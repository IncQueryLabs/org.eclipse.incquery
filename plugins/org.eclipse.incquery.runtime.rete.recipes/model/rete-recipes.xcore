@Ecore(nsURI="http://www.eclipse.org/incquery/rete/recipes")
package org.eclipse.incquery.runtime.rete.recipes

// TODO missing things:
// - transitive closure - DONE
// - more / better traceability to pattern language, expressions

// Base concepts

/*
 * Container for Rete recipes.
 */
class ReteRecipe {
	contains ReteNodeRecipe[] recipeNodes
}

/*
 * Abstract base class for model elements that represent "Rete node recipes",
 * that is DTOs that carry information for Rete network construction.
 */
abstract class ReteNodeRecipe {
	/*
	 * Temporary construct for storing traceability information.
	 */
	String traceInfo
}

/*
 * Abstract base class for single-parent node recipes.
 */
abstract class SingleParentNodeRecipe extends ReteNodeRecipe {
	refers ReteNodeRecipe parent
}

/*
 * Abstract base class for alpha node recipes.
 */
abstract class AlphaRecipe extends SingleParentNodeRecipe {}


abstract class MultiParentNodeRecipe extends ReteNodeRecipe {
	refers ReteNodeRecipe[] parents
}

/*
 * Represents nodes that enforce tuple uniqueness, i.e. filter out
 * duplicate input tuples for output.
 */
class UniquenessEnforcerRecipe extends MultiParentNodeRecipe { } 

/*
 * The production node represents the output of the Rete network,
 * from which the results of a query can be read.
 */
class ProductionRecipe extends MultiParentNodeRecipe {
	/*
	 * Traceability link to defining pattern object (from EMFPatternLanguage)
	 */
	Object pattern
}


/*
 * Represents helper nodes that provide projection indexing for Beta nodes.
 */
class ProjectionIndexer extends SingleParentNodeRecipe {
	contains Mask mask
}

/*
 * Abstract base class for Beta node recipes.
 */
abstract class BetaRecipe extends ReteNodeRecipe {
	contains ProjectionIndexer leftParent
	contains ProjectionIndexer rightParent
	// TODO complementer mask?
}


// helper concepts: masks and indices

/*
 * A mask defines the set of tuple variables that need to be taken into consideration for operations.
 */
class Mask {
	/*
	 * The indices that are relevant for tuple operations.
	 */
	Index[] sourceIndices
	/*
	 * The arity of tuples.
	 */
	int sourceArity // TODO move to NodeRecipe instead?
}
/*
 * Indexes tell which variable of tuples are relevant for a given operation.
 * TODO: is this necessary at all?
 */
type Index wraps Integer

class StringIndexMapEntry wraps java.util.Map$Entry{
	String key
	Index value
}

// Input and output

/*
 * Represents input nodes for the Rete network, i.e. nodes
 * that generate input tuples for processing.
 */
abstract class InputRecipe extends ReteNodeRecipe {
	/*
	 * Temporary construct for identifying types.
	 * TODO improve type references
	 */
	String typeIdentifier
}

class UnaryInputRecipe extends InputRecipe {}

class BinaryInputRecipe extends InputRecipe {}

/*
 * Simple node that stores constant values.
 */
class ConstantRecipe extends ReteNodeRecipe {
	/*
	 * Stores constant values. May be empty.
	 * 
	 * TODO store constants as strings instead? (for easier serialization)
	 */
	Object[] constantValues
}

// Simple nodes

/*
 * Represents transitive closure.
 */
class TransitiveClosureRecipe extends AlphaRecipe { }

/*
 * Abstract base class for nodes that implement filtering operations.
 */
abstract class FilterRecipe extends AlphaRecipe { }
class InequalityFilterRecipe extends FilterRecipe {
	Index subject
	Index[] inequals
}
class EqualityFilterRecipe extends FilterRecipe {
	Index[] indices
}

/*
 * Implements projection without uniqueness checking.
 */
class TrimmerRecipe extends AlphaRecipe {
	contains Mask mask
}

// Expressions

/*
 * Abstract base class for Rete nodes that evaluate an expression.
 */
abstract class ExpressionEnforcerRecipe extends AlphaRecipe {
	/*
	 * Provides traceability to expression representation.
	 */
	Object expression
	/*
	 * String -> Index map.
	 * Maps variable names in the expression to tuple indices.
	 */
	contains StringIndexMapEntry[] mappedIndices
}
/*
 * Computes the result of the boolean expression evaluation and
 * only passes tuples for which the result is true.
 */
class CheckRecipe extends ExpressionEnforcerRecipe {}
/*
 * Computes the result of expression evaluation and suffixes the result
 * to output tuples as the last element.
 */
class EvalRecipe extends ExpressionEnforcerRecipe {}



// Aggregators

/*
 * Represents a (compound) node that performs an aggregation
 * operation.
 */
abstract class AggregatorRecipe extends AlphaRecipe {}

/*
 * The count aggregator node represents a "cound find" operation.
 */
class CountAggregatorRecipe extends AggregatorRecipe {}

// Joins 

/*
 * The most basic beta operation, the join node performs a join operation over two input tuple sets.
 */
class JoinRecipe extends BetaRecipe {}

/*
 * Existence joins are TODO
 * 
 * See http://en.wikipedia.org/wiki/Relational_algebra
 */
abstract class ExistenceJoinRecipe extends BetaRecipe {}

/*
 * A semi-join is TODO
 * 
 * See http://en.wikipedia.org/wiki/Relational_algebra
 */
class SemiJoinRecipe extends ExistenceJoinRecipe {}

/*
 * An anti-join is TODO
 * 
 * See http://en.wikipedia.org/wiki/Relational_algebra 
 */
class AntiJoinRecipe extends ExistenceJoinRecipe {}

// Safe aggregator compounds

/*
 * An aggregator join node is a beta node that performs an aggregation operation.
 */
class AggregatorJoinRecipe extends BetaRecipe {
	contains AggregatorRecipe aggregator
}

